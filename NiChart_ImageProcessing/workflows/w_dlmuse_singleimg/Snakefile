'''
Snakemake file for NiChart MRI processing workflow
 
Input data: MRI images organized in folder structure: data/init_mri/MRI/{study}/{fname}.nii.gz
'''

configfile: "./config/config.yaml"
workdir: '.'

import os
STUDIES = os.listdir("../../data/MRI")

rule ALL:
    input: 
        expand("../../data/DLMUSE/{study}/{study}_DLMUSE.csv", study=STUDIES)
        
rule nichart_dlmuse:
    '''
    Rule to run DLMUSE
     DLMUSE uses the raw T1 as input and generates the roi image and the csv file with ROI volumes
    '''
    input:
        "../../data/MRI/{study}/{fname}.nii.gz"
    output:
        img=protected("../../data/DLMUSE/{study}/{fname}_dlmuse.nii.gz"),
        csv=temp("../../data/DLMUSE/{study}/{fname}_dlmuse.csv")
    resources:
        cpus_per_task=8,
        mem_mb=32000,
        slurm_extra="--ntasks-per-node=4"
    shell:
        "bash ./utils/run_nichart_dlmuse.sh {input} {output}"

def get_in_files(wildcards):
    '''
    Returns a list of image files inside the input folder
    '''
    in_files = glob_wildcards("../../data/MRI/" + wildcards.study + "/{fname}.nii.gz")
    in_paths = ['../../data/DLMUSE/' + wildcards.study + '/' + 
                x + '_dlmuse.csv' for x in in_files.fname]
    return in_paths
    
rule combine_vol:
    '''
    Combines all roi csv files for a study
    '''
    input:
        get_in_files
    output:
        protected("../../data/DLMUSE/{study}/{study}_DLMUSE.csv")
    resources:
        mem_mb=8000,
    shell:
        "bash ./utils/run_combine_ROIs.sh {output} {input}"

